#!/usr/bin/env perl
# 
# process all subdirectories of the name NNNNNNNN-NNNN (YYYYMMDD-HHMM)
#
# pick up the invidual tl*.jpg files
# from different restarts of the timelapse script due to reboots
#
# use 'convert' to overlay each picture with the ExifDateTime value
#
# store the results in directory $exif
#
# make sure the resulting files are numbered starting with tl000001.jpg without
# a gap to prepare for the later creation of the timelapse video using
# 'avconv' from the libav or ffmpeg project
#

use strict;
use warnings;
use POSIX;
use Data::Dump qw(dump);
use Getopt::Std;

my $font;

if ("$^O" eq "MSWin32") {
	$font = "Envy-Code-R";
} else {
	$font = "EnvyCodeR";
}

sub get_dirs {
	my $cwd = ".";
	my @dirs = ();
	opendir(CWD, $cwd) or die $!;
	while(my $dir = readdir(CWD)) {
# print $dir, "\n";
		next unless $dir =~ /\d{8}\-\d{4}/;
		push(@dirs, $dir);
	}
	closedir(CWD);
	return @dirs;
}

sub max_number($) {
	my ($dir) = @_;
	my $max = 0;
	my $jpg = "";
	opendir(DIR, $dir) or die $!;
	while (my $file = readdir(DIR)) {
# print $file, "\n";
		($jpg) = $file =~ m/(\d+).jpg$/;
		next if !defined($jpg);
# printf "%d\n", $jpg;
		if ($jpg > $max) { $max = $jpg; }
	}
	closedir(DIR);
	return $max +0;
}

# accept start and end time in form HHMM
my %opts;
getopts('s:e:', \%opts);
my $beginn = ($opts{'s'} || "0000") . "00";
my $ende   = ($opts{'e'} || "2359") . "59";

my $exif = "exif-$beginn-$ende"; # output directory
mkdir $exif;
my @dirs = get_dirs();
my $number = max_number($exif) + 1;

foreach my $dir (sort(@dirs)) {
	print "process dir=", $dir, "\n";
	opendir(TL, $dir) or die $!;
	my @files = ();
	while(my $jpg = readdir(TL)) {
		next unless $jpg =~ /tl\d+.jpg$/;

		my $date = POSIX::strftime("%H%M%S", localtime((stat "$dir/$jpg")[9]));

		# print "$date >= $beginn && $date <= $ende?\n";
		next unless ($date >= $beginn && $date <= $ende);
		# print "$dir/$jpg ($date)\n";

		push(@files, $jpg);
	}
	foreach my $jpg (sort(@files)) {
		my $tgt = sprintf("tl%06d.jpg", $number);
		print "convert $jpg to $exif/$tgt\n";
		my $rc = system("convert $dir/$jpg -font \"$font\" -gravity SouthEast -pointsize 24 -fill Magenta -annotate +12+12 \"%[EXIF:DateTimeOriginal]\" $exif/$tgt");
		if ($rc == 0) {
			my @atime = (stat("$dir/$jpg"));
			utime $atime[8], $atime[9], "$exif/$tgt";
			$number++;
		} else { # error
			print "error converting file $tgt\n";
		}
	}
	closedir(TL);
	print "\n";
}

